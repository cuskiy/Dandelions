{ ... }:
{
  schema.software = {
    extra = false;
  };

  traits.software =
    { lib, pkgs, schema, ... }:
    let
      cfg = schema.software;
    in
    {
      environment.systemPackages =
        with pkgs;
        [
          disko
          file
          gnupg
          ncdu
          nh
          nix-output-monitor
          nixfmt-tree
          p7zip
          parted
          tree
          unrar-free
          unzip
          wget
          zip
        ]
        ++ lib.optionals cfg.extra [
          (bottles.override { removeWarningPopup = true; })
          (chromium.override {
            commandLineArgs = [
              # Four years ago, someone tried enabling touchpad history navigation
              # as the default in Chromium on Linux.
              # To this day, it can still only be enabled in this way.
              # https://chromium-review.googlesource.com/c/chromium/src/+/3955902
              "--enable-features=TouchpadOverscrollHistoryNavigation,WaylandWindowDecorations"
              "--ozone-platform-hint=auto"
            ];
          })
          fractal
          # ladybird
          # libreoffice
          mpv
          obs-studio
          (symlinkJoin {
            name = "qbittorrent";
            paths = [ qbittorrent ];
            buildInputs = [ makeWrapper ];
            postBuild = ''
              wrapProgram $out/bin/qbittorrent --set QT_QPA_PLATFORMTHEME "gtk3"
            '';
          })
          (symlinkJoin {
            name = "signal-desktop";
            paths = [ signal-desktop ];
            buildInputs = [ makeWrapper ];
            postBuild = ''
              wrapProgram $out/bin/signal-desktop \
                --set HTTPS_PROXY "socks5://localhost:1024" \
                --set HTTP_PROXY "socks5://localhost:1024"
            '';
          })
          thunderbird
          tor-browser
          # vscodium
          zed-editor

          android-tools
          appimage-run
          dnsutils
          lsof
          nil
          nixd
          qrencode
          rdap
          steam-run-free
          whois
        ];

      programs = {
        gnupg.agent = {
          enable = true;
          pinentryPackage = pkgs.pinentry-tty;
        };
        git = {
          enable = true;
          config = {
            core.autocrlf = "input";
            core.editor = "vim";
            merge.autoStash = true;
            pull.autoStash = true;
            pull.rebase = true;
            rebase.autoStash = true;
            safe.directory = "*";
          }
          // lib.optionalAttrs cfg.extra {
            commit.gpgSign = true;
            http.proxy = "socks5://localhost:1024";
            https.proxy = "socks5://localhost:1024";
            tag.gpgSign = true;
            user.email = "main@saltedfishes.com";
            user.name = "SaltedFishes";
            user.signingkey = "0F3659EAC9207CE3B0D7C5ABA4CA90D326E9FB7C";
          };
        };
        htop = {
          enable = true;
          settings = {
            color_scheme = 6;
            hide_userland_threads = true;
            highlight_base_name = true;
            highlight_changes = true;
            shadow_other_users = true;
            show_cpu_temperature = true;
            show_program_path = false;
            tree_view = true;
          };
        };
        mtr.enable = cfg.extra;
        nix-ld.enable = true;
        vim = {
          enable = true;
          defaultEditor = true;
        };
      };

      services = {
        envfs.enable = true;
        flatpak.enable = cfg.extra;
      };
    };
}
